apply plugin: 'com.android.application'

android {
    signingConfigs {
        debug {
            keyAlias 'debug'
            keyPassword 'password'
            storeFile file('keystore.jks')
            storePassword 'password'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 27

    defaultConfig {
        applicationId "uk.co.dancetrix"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode vCode as Integer
        versionName vName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "OVERRIDE_TO_EMAIL", 'null'
        buildConfigField "boolean", "MOCK_SERVICES", 'false'
        buildConfigField "boolean", "MOCK_EMAIL", 'false'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "variant"

    productFlavors {
        local {
            dimension "variant"
            buildConfigField "String", "OVERRIDE_TO_EMAIL", "\"$SECRET_EMAIL\""
        }
        mock {
            dimension "variant"
            buildConfigField "boolean", "MOCK_SERVICES", 'true'
            buildConfigField "boolean", "MOCK_EMAIL", 'true'
        }
        live {
            dimension "variant"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        // For release, only build the live variant
        if (variant.buildType.name == "release" && !names.contains("live")) {
            variant.ignore = true
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-config:16.0.1'
    implementation 'com.google.firebase:firebase-storage:16.0.3'
    implementation 'com.google.firebase:firebase-auth:16.0.4'
    implementation 'commons-io:commons-io:2.4'
    implementation 'com.github.dariopellegrini:FormBuilder:v0.9'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.samskivert:jmustache:1.14'
    implementation "ru.noties:markwon:2.0.0"
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
